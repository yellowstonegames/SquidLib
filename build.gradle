apply plugin: 'java'
// note from smelC: starting with Eclipse Neon, you should not use
// gradle's Eclipse plugin (grade :eclipse). Instead you should use Eclipse's buildship project (shipped by default with Eclipse):
// https://projects.eclipse.org/projects/tools.buildship
// buildship takes care of generating .classpath and .project files according to your build.gradle files
// (right-click on a project > Gradle > Refresh Gradle Project).

//Support UTF-8 file encoding even on Windows, where it defaults to something terrible.
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' and Maven Central for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
}

project.ext.assetsDir = new File("assets");

sourceSets {
    main {
        java {
            srcDirs 'squidlib/src/main/java'
            srcDirs 'squidlib-util/src/main/java'
            srcDirs 'squidlib-extra/src/main/java'

            // srcDirs 'squidlib-util/etc' <- No. Solely contains TilesetsGenerator
	    // which isn't useful anymore.
        }
        resources {
            srcDirs 'squidlib-util/src/main/resources'
        }
    }
    test {
    	java {
            srcDirs 'squidlib/src/test/java'
            srcDirs 'squidlib-util/src/test/java'
            srcDirs 'squidlib-extra/src/test/java'
            // srcDirs 'squidlib-performance/src/main/java' <- requires org.openjdk
    	}
        resources {
            srcDirs 'squidlib/src/test/resources'
            srcDirs 'squidlib-extra/src/test/resources'
        }
    }
}

// define 'gdxVersion' if not defined yet. This allows
// a project depending on SquidLib (like: your game) to define
// 'gdxVersion' itself, and yet be able to build SquidLib standalone too.
def gdxVersion = System.getenv("gdxVersion") ?: "1.9.8" // As in pom.xml
project.sourceCompatibility = 1.7
// project.sourceCompatibility = '1.6'  // For android. Commented because not honored (diamonds) :-(

// In this section you declare the dependencies for your production and test code
dependencies {
	compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.github.tommyettinger:regexodus:0.1.9"
	testCompile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
	testCompile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	testCompile 'junit:junit:4.12'
}

// Execute with 'gradle everythingDemo' (add --info to get debug info)
task(everythingDemo, type: JavaExec) {
    main = 'squidpony.gdx.examples.desktop.EverythingDemoLauncher'
    // smelC: It would be nice to automatically add the three following
    // lines to any 'JavaExec' task, but I could not find how to do it.
    classpath(sourceSets.test.runtimeClasspath,project.assetsDir)
    standardInput = System.in
    ignoreExitValue = true
}

// Execute with 'gradle basicDemo' (add --info to get debug info)
task(basicDemo, type: JavaExec) {
    main = 'squidpony.gdx.examples.desktop.BasicDemoLauncher'
    classpath(sourceSets.test.runtimeClasspath,project.assetsDir)
    standardInput = System.in
    ignoreExitValue = true
}

// Execute with 'gradle iconDemo' (add --info to get debug info)
task(iconDemo, type: JavaExec) {
    main = 'squidpony.gdx.tests.IconsTest'
    classpath(sourceSets.test.runtimeClasspath,project.assetsDir)
    standardInput = System.in
    ignoreExitValue = true
}

// Execute with 'gradle dataDemo' (add --info to get debug info)
task(dataDemo, type: JavaExec) {
    main = 'squidpony.DataDemo'
    classpath(sourceSets.test.runtimeClasspath,project.assetsDir)
    standardInput = System.in
    ignoreExitValue = true
}
